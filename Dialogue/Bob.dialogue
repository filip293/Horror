~ start

Bob: [[Why, hello there youngin'!|Hey there buster!|Greetings my young friend!]]
Bob: What brings you here?
You: My car broke down while I was driving through this town.
You: Could you help me?
Bob: Oh, that's just terrible! 
Bob: I can tow it with my truck that's in the front, but you'll have to help me in return.
Bob: Why don't you stick around and help for now, seeing as you don't have anything to do?
- Yeah, sure. Why not.
	Bob: Great! 
	Bob: I guess you can go ahead and start with cleaning the floors!
	Bob: The broom is in the back.
	set global.route = "BOB"
	set global.chosen = true
	=> END
- It’s alright, I’ll just stay at a motel.
	Bob: I wouldn't do that if I were you...
	Bob: Rumors have been going around about a mysterious figure that comes out around this time of night.
	Bob: People have gone missing, but no ones been found. 
	Bob: Why don't you reconsider?
	- Yeah... sure man.
		Bob: Great! 
		Bob: I guess you can go ahead and start with cleaning the floors!
		Bob: The broom is in the back.
		set global.route = "BOB"
		set global.chosen = true
		=> END
	- No, really, I'll be fine!
		Bob: Alright, best of luck then.
		Bob: There's a motel nearby, feel free to check the map for directions.
		Bob: But don't say I didn't warn you...
		set global.route = "Motel"
		set global.chosen = true
=> END

~ wait
Bob: Come back to me once you've finished cleaning the floors. \nThe dust ain't gonna clean up itself ya know?
=> END

~ wait1
Bob: The floors are looking spotless, go ahead and leave the broom where you found it and come back to me.
=> END

~ finished_sweeping
Bob: Wow, good job. The floors are looking spotless! 
Bob: Next, could you go ahead and collect the coins from the machines?
set global.bobgiveskey = true
Bob: Here's the key to the machines. 
Bob: There’s a basket for the coins next to me.
Bob: My back is hurting quite a bit, come to the back and get it yourself. 
=> END

~ wait2
if global.bobannoyed <= 2
	Bob: As I said, my back's hurting, I cannot assist you any further.
	do global.bobannoyed += 1
	=> END
if global.bobannoyed >= 3 and global.bobannoyed <= 5
	Bob: Listen here [[youngin|youngster|trickster]]. I can't help you out here.
	do global.bobannoyed += 1
	=> END
if global.bobannoyed >= 6 and global.bobannoyed <= 10
	Bob: ...
	do global.bobannoyed += 1
	=> END
if global.bobannoyed >= 11
	Bob: ALRIGHT, I'VE HAD ENOUGH OF YA.
	set global.bobshotgun = true
	=> END

~ wait3
Bob: The basket ain't gonna fill itself youngin. 
=> END

~ bob_gone
You: What the? [next=1]
You: Hey [[old man|Bob]], [wait=1]damn it, where is he now?
=> END